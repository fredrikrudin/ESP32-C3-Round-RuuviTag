#include <Arduino.h>
#include <TFT_eSPI.h>
#include <lvgl.h>
#include "wifi_manager.h"
#include "ruuvi_ble.h"

void gui_init_ruuvi_hub(void);

TFT_eSPI tft = TFT_eSPI();
static lv_disp_draw_buf_t draw_buf;
static lv_color_t buf[240 * 10];

void my_disp_flush(lv_disp_drv_t *disp, const lv_area_t *area, lv_color_t *color_p) {
    uint32_t w = (area->x2 - area->x1 + 1);
    uint32_t h = (area->y2 - area->y1 + 1);
    tft.startWrite();
    tft.setAddrWindow(area->x1, area->y1, w, h);
    tft.pushColors((uint16_t *)&color_p->full, w * h, true);
    tft.endWrite();
    lv_disp_flush_ready(disp);
}

void setup() {
    Serial.begin(115200);

    // Initialize display hardware and LVGL core
    tft.init();
    tft.setRotation(0);
    lv_init();
    lv_disp_draw_buf_init(&draw_buf, buf, NULL, 240 * 10);

    static lv_disp_drv_t disp_drv;
    lv_disp_drv_init(&disp_drv);
    disp_drv.hor_res = 240;
    disp_drv.ver_res = 240;
    disp_drv.flush_cb = my_disp_flush;
    disp_drv.draw_buf = &draw_buf;
    lv_disp_drv_register(&disp_drv);

    // Initialize sub-modules
    gui_init_ruuvi_hub();
    init_wifi_manager();
    init_ruuvi_ble();
}

void loop() {
    lv_timer_handler();      // Handle LVGL task engine
    tick_wifi_manager();     // Process network connection and time synch
    tick_ruuvi_ble();        // Process scheduled Bluetooth scanning
    delay(5);
}
